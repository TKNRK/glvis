# vertex shader
# version 430

uniform mat4 MV;
uniform mat4 P;
uniform float pointsize;

layout (location=0) in vec3 position_vs;

flat out int   vertexID_fs;
flat out float vertexZ_fs;

void main() {
  vec4 p = vec4(position_vs, 1);
  vec4 p_mv = MV * p;
  gl_Position = P * p_mv;

  gl_PointSize = pointsize / (-p_mv.z);

  vertexID_fs = gl_VertexID;
  vertexZ_fs  = p_mv.z;
}

# fragment shader
# version 430

const   vec4 WHITE = vec4(1, 1, 1, 1);
const   vec4 RED   = vec4(1, 0, 0, 1);
layout (origin_upper_left) in vec4 gl_FragCoord;
flat in int   vertexID_fs;
flat in float vertexZ_fs;

out vec4 color_fb;

layout (std430, binding = 0) buffer SSB {
  // Input fields
  uint  clicked_x, clicked_y;
  // Internally used fields
  float pick_z;
  int   pick_lock;
  // Output field
  int   pick_id;
};

const int UNLOCKED = 0, LOCKED = 1;

subroutine void Fragment(float);
subroutine uniform Fragment fragment;

subroutine (Fragment)
void pick(float r) {
  if (int(gl_FragCoord.x) == clicked_x && int(gl_FragCoord.y) == clicked_y) {
    while (pick_z < vertexZ_fs) {
      if (atomicCompSwap(pick_lock, UNLOCKED, LOCKED) == UNLOCKED) {
        pick_z = vertexZ_fs;
        pick_id = vertexID_fs;
        pick_lock = UNLOCKED;
      }
    }
  }
}

subroutine (Fragment)
void paint(float r) {
  if (abs((gl_FragCoord.x - clicked_x) * (gl_FragCoord.y - clicked_y)) < 5) color_fb = WHITE;
  else color_fb = vec4(vec3(r) * 0.5, 1) + ((pick_id == vertexID_fs) ? RED : WHITE) * 0.5;
}

void main() {
  vec2 offset = vec2(gl_PointCoord * 2.0 - 1.0);
  if (1.0 - dot(offset, offset) < 0) discard;
  float r = sqrt(1 - dot(offset, offset));
  fragment(r);
}